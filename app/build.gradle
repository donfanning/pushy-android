/*
 * Copyright (c) 2016-2017, Michael A. Updike All rights reserved.
 * Licensed under Apache 2.0
 * https://opensource.org/licenses/Apache-2.0
 * https://github.com/Pushy-Clipboard/pushy-android/blob/master/LICENSE.md
 */

apply plugin: 'com.android.application'

// Load keystore
def keystorePropertiesFile = rootProject.file("secure/keystore.props")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

//noinspection GroovyMissingReturnStatement
android {
    signingConfigs {
        config {
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    compileSdkVersion rootProject.sdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.weebly.opus1269.clipman"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.sdkVersion
        versionCode 15004
        versionName "1.3.0"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            debuggable true
        }
    }

    configurations {
        compile.exclude group: "org.apache.httpcomponents", module: "httpclient"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    // third party open source
    compile 'joda-time:joda-time:2.9.2'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.androidessence:recyclerviewcursoradapter:1.0.0'
    // various fixes for support:preference-v7
    compile "com.takisoft.fix:preference-v7:${rootProject.ext.supportLibraryVersion}.0"

    // android support libraries
    compile "com.android.support:support-v4:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:recyclerview-v7:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibraryVersion}"
    compile "com.android.support:support-vector-drawable:${rootProject.ext.supportLibraryVersion}"

    // Google Play service
    compile "com.google.android.gms:play-services-analytics:${rootProject.ext.gmsVersion}"
    compile "com.google.android.gms:play-services-identity:${rootProject.ext.gmsVersion}"
    compile "com.google.android.gms:play-services-auth:${rootProject.ext.gmsVersion}"
    // firebase - keep in sync with play-services dependencies
    compile "com.google.firebase:firebase-core:${rootProject.ext.gmsVersion}"
    compile "com.google.firebase:firebase-messaging:${rootProject.ext.gmsVersion}"
    compile "com.google.firebase:firebase-auth:${rootProject.ext.gmsVersion}"
    compile "com.google.firebase:firebase-crash:${rootProject.ext.gmsVersion}"
    compile 'com.firebase:firebase-jobdispatcher:0.8.1'

    // Google API
    compile "com.google.api-client:google-api-client:${rootProject.ext.gapiVersion}"
    compile "com.google.api-client:google-api-client-android:${rootProject.ext.gapiVersion}"
    compile "com.google.apis:google-api-services-people:v1-rev4-${rootProject.ext.gapiVersion}"

    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2", {
        exclude group: "com.android.support", module: "support-annotations"
    })
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    testCompile 'junit:junit:4.12'
}

// must be at end
apply plugin: "com.google.gms.google-services"
